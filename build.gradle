buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: "jacoco"
apply plugin: 'idea'
apply plugin: 'eclipse'

group = 'com.nisumlatam'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    acceptanceTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/acceptanceTest/java')
        }
        resources.srcDir file('src/acceptanceTest/resources')
    }
}

configurations {
    acceptanceTestCompile.extendsFrom testCompile
    acceptanceTestRuntime.extendsFrom testRuntime
}

task acceptanceTest(type: Test) {
    testClassesDirs = files(file("${buildDir}/classes/java/acceptanceTest"))
    classpath = sourceSets.acceptanceTest.runtimeClasspath
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-amqp")
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('org.springframework.boot:spring-boot-starter-web')

    testCompile 'junit:junit:4.12'
    testCompile('org.springframework.boot:spring-boot-starter-test')

    acceptanceTestCompile('info.cukes:cucumber-core:1.2.5')
    acceptanceTestCompile('info.cukes:cucumber-java:1.2.5')
    acceptanceTestCompile('info.cukes:cucumber-junit:1.2.5')
    acceptanceTestCompile('info.cukes:cucumber-spring:1.2.5')

    provided group: 'de.flapdoodle.embed', name: 'de.flapdoodle.embed.mongo', version: '2.0.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5'
}
